import Mathlib.Logic.ExistsUnique
import Mathlib.Logic.Equiv.Defs
import Mathlib.Logic.Equiv.Nat
import Mathlib.Data.Rat.Denumerable
import Mathlib.Tactic.Group
import Mathlib.Algebra.Group.Defs
import Mathlib.CategoryTheory.Category.Basic
import Mathlib.CategoryTheory.Category.Cat.Terminal
import Mathlib.CategoryTheory.Iso
import Mathlib.LinearAlgebra.Matrix.NonsingularInverse
import Cat.L1
import Cat.L2Live
import Cat.Product

open CategoryTheory Limits

universe u v

variable {ğ“’ : Type u} [Category ğ“’] {A B X Y Z Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ Zâ‚ Zâ‚‚ : ğ“’}

section terminal

@[grind]
structure PSTrans where
  S : Type u
  Ïƒ : S â†’ Option S

@[grind]
structure PSHom (S T : PSTrans) where
  f : S.S â†’ T.S
  h : (Option.map f) âˆ˜ S.Ïƒ = T.Ïƒ âˆ˜ f

section examples

def us : PSTrans where
  S := PUnit
  Ïƒ := Option.some

def un : PSTrans where
  S := PUnit
  Ïƒ := fun _ => Option.none

example : PSHom us un where
  f := id
  h := funext fun | .unit => by simp [un, us]

example : PSHom un us where
  f := id
  h := funext fun | .unit => by simp [un, us]

def bSsSs : PSTrans where
  S := Bool
  Ïƒ := Option.some

def bNN : PSTrans where
  S := Bool
  Ïƒ := fun _ => Option.none

def bSsN : PSTrans where
  S := Bool
  Ïƒ := fun | .true => .some .true | .false => .none

def bSdN : PSTrans where
  S := Bool
  Ïƒ := fun | .true => .some .false | .false => .none

def bNSs : PSTrans where
  S := Bool
  Ïƒ := fun | .false => .some .false | .true => .none

def bNSd : PSTrans where
  S := Bool
  Ïƒ := fun | .false => .some .true | .true => .none

example : PSHom bSdN bNSd where
  f := not
  h := funext fun | .true | .false => by simp [bSdN, bNSd]

example : PSHom bSsN bNSs where
  f := not
  h := funext fun | .true | .false => by simp [bSsN, bNSs]

example : PSHom bNN bSsN where
  f := fun _ => .false
  h := funext fun | .true | .false => rfl

end examples

instance : Category PSTrans where
  Hom := PSHom
  id X := âŸ¨id, funext fun v => by grindâŸ©
  comp {X Y Z} A B := âŸ¨B.f âˆ˜ A.f, calc
    _ = Option.map B.f âˆ˜ Option.map A.f âˆ˜ X.Ïƒ := by rw [â†Option.map_comp_map, Function.comp_assoc]
    _ = (Option.map B.f âˆ˜ Y.Ïƒ) âˆ˜ A.f          := by rw [A.h, â†Function.comp_assoc]
    _ = Z.Ïƒ âˆ˜ B.f âˆ˜ A.f                       := by rw [B.h, Function.comp_assoc]âŸ©

@[grind]
structure Conat : Type u where
  f : Nat â†’ Bool
  stops : âˆ€ n, (f n) = .false â†’ (f n.succ) = .false

namespace Conat

def step (x : Conat.{u}) : Conat.{u} where
  f := x.f âˆ˜ .succ
  stops n h := x.stops n.succ h

theorem step_many {x : Conat}
    : {n : Nat} â†’ n.repeat step x = âŸ¨(x.f <| n + Â·), fun _ h => x.stops _ hâŸ©
  | 0 => by simp [Nat.repeat]
  | n+1 => by
    simp only [Nat.repeat, step, step_many, mk.injEq]
    grind

def dest (x : Conat.{u}) : Option Conat.{u} :=
  match x.f 0 with
  | .true => .some x.step
  | .false => .none

def corec.f {X : Type v}
    (gen : X â†’ Option X)
    (g : X)
    : Nat â†’ Bool 
  | 0   => 
    match gen g with 
    | .some _ => .true
    | .none   => .false
  | n+1 => match gen g with 
    | .some g => corec.f gen g n
    | .none   => .false

def corec {X : Type v}
    (gen : X â†’ Option X)
    (g : X)
    : Conat where
  f := corec.f gen g
  stops n h := by 
    induction n generalizing g
    Â· dsimp [corec.f] at h âŠ¢
      grind
    case succ ih => 
      dsimp [corec.f] at h âŠ¢
      split at h
      Â· specialize ih _ h
        dsimp [corec.f] at ih
        split at ih
        Â· exact ih
        Â· rfl
      Â· rfl

def corec_dest
    {X : Type v}
    (gen : X â†’ Option X)
    (g : X)
    : (Conat.corec gen g).dest = Option.map (Conat.corec gen) (gen g) := by
  dsimp [dest, corec, Option.map, corec.f]
  cases h : gen g
  Â· rfl
  Â· rename_i val
    simp only [step, Option.some.injEq, mk.injEq]
    funext n
    dsimp
    induction n
    Â· simp [corec.f, h]
    case succ ih => 
      dsimp [corec.f] at ih
      conv =>
        lhs
        dsimp [corec.f]
        rw [h]
        dsimp
      split at ih
      <;> rename_i heq
      Â· obtain rfl := (Option.some.injEq _ _).mp (h.symm.trans heq)
        rfl
      Â· exact Option.noConfusion (h.symm.trans heq)

def Bisim.Is (R : Conat â†’ Conat â†’ Prop) : Prop :=
  âˆ€ a b, R a b â†’ Option.Rel R a.dest b.dest

def Bisim (a b : Conat) : Prop := âˆƒ R, Bisim.Is R âˆ§ R a b

theorem bisim {a b : Conat}
    (h : Bisim a b) : a = b := by
  rcases h with âŸ¨R, his, rabâŸ©
  obtain h := his _ _ rab
  have : âˆ€ n : Nat, Option.Rel R
      (n.repeat Conat.step a).dest
      (n.repeat Conat.step b).dest := by
    intro n
    induction n
    Â· exact h
    case succ n ih =>
      dsimp [Nat.repeat, dest, step] at ih
      split at ih
      <;> rename_i heq
      Â· split at ih
        case h_2 => cases ih
        rcases ih with âŸ¨ihâŸ©
        exact his _ _ ih
      Â· dsimp [dest, step, Nat.repeat]
        rw [(Nat.repeat step n a).stops 0 heq]
        split at ih
        Â· cases ih
        rename_i heq
        rw [(Nat.repeat step n b).stops 0 heq]
        exact .none
  clear h
  rcases a with âŸ¨a, haâŸ©
  rcases b with âŸ¨b, hbâŸ©
  refine (mk.injEq _ _ _ _).mpr <| funext fun n => ?_
  induction n
  Â· specialize this 0
    dsimp [dest] at this
    split at this
    all_goals split at this
    any_goals cases this
    all_goals simp_all only [Nat.repeat]
  case succ n ih =>
    specialize this n
    simp only [dest, step_many, add_zero] at this
    split at this
    Â· rename_i heq
      rw [â†ih, heq] at this
      rcases this with âŸ¨thisâŸ©
      have := his _ _ this
      simp [dest, step] at this
      clear *-this
      split at this
      <;> split at this
      <;> simp_all only [Option.rel_some_some,
        Option.not_rel_some_none, Option.not_rel_none_some,
        Option.rel_none_none]
    Â· grind

end Conat

instance : HasTerminal PSTrans :=
  IsTerminal.hasTerminal
    (X := âŸ¨Conat, Conat.destâŸ©)
    <| IsTerminal.ofUniqueHom
      (fun x => {
        f := Conat.corec x.Ïƒ
        h := funext fun v => (Conat.corec_dest x.Ïƒ v).symm
      })
      fun x âŸ¨m, hâŸ© => (PSHom.mk.injEq _ _ _ _).mpr
        (funext fun v => Conat.bisim 
          âŸ¨
            (âˆƒ u, Â· = m u âˆ§ Â· = Conat.corec x.Ïƒ u), 
            by
              rintro a b âŸ¨w, rfl, rflâŸ©
              rw [ Conat.corec_dest,
                â† show Option.map _ _ = (m w).dest from funext_iff.mp h w]
              cases x.Ïƒ w
              Â· exact .none
              Â· exact .some âŸ¨_, rfl, rflâŸ©,
            âŸ¨v, rfl, rflâŸ©
          âŸ©
        )

end terminal

section initial

instance : HasInitial PSTrans :=
  IsInitial.hasInitial
    (X := âŸ¨PEmpty, fun _ => .noneâŸ©)
    <| IsInitial.ofUniqueHom
      (fun _ => âŸ¨fun v => v.elim, funext fun v => v.elimâŸ©)
      (fun _ âŸ¨_, _âŸ© =>
        (PSHom.mk.injEq _ _ _ _).mpr (funext fun v => v.elim))

end initial

section product

section ex1

variable
    [HasBinaryProduct Xâ‚ Xâ‚‚]
    [HasBinaryProduct Yâ‚ Yâ‚‚]
    [HasBinaryProduct Zâ‚ Zâ‚‚]

theorem ex1.a (f : X âŸ¶ Y) (gâ‚ : Y âŸ¶ Zâ‚) (gâ‚‚ : Y âŸ¶ Zâ‚‚)
    : f â‰« prod.lift gâ‚ gâ‚‚ = prod.lift (f â‰« gâ‚) (f â‰« gâ‚‚) := by
  ext
  Â· calc
      (f â‰« prod.lift gâ‚ gâ‚‚) â‰« prod.fst
        = f â‰« (prod.lift gâ‚ gâ‚‚ â‰« prod.fst)        := Category.assoc _ _ _
      _ = f â‰« gâ‚                                  := by rw [prod.lift_fst]
      _ = prod.lift (f â‰« gâ‚) (f â‰« gâ‚‚) â‰« prod.fst  := by rw [prod.lift_fst]
  Â· calc
      (f â‰« prod.lift gâ‚ gâ‚‚) â‰« prod.snd
        = f â‰« (prod.lift gâ‚ gâ‚‚ â‰« prod.snd)        := Category.assoc _ _ _
      _ = f â‰« gâ‚‚                                  := by rw [prod.lift_snd]
      _ = prod.lift (f â‰« gâ‚) (f â‰« gâ‚‚) â‰« prod.snd  := by rw [prod.lift_snd]

lemma map_def
    (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚)
    : prod.map fâ‚ fâ‚‚ = prod.lift (prod.fst â‰« fâ‚) (prod.snd â‰« fâ‚‚) := 
  (prod.lift_fst_comp_snd_comp fâ‚ fâ‚‚).symm

theorem ex1.b
    (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚)
    (gâ‚ : Z âŸ¶ Xâ‚) (gâ‚‚ : Z âŸ¶ Xâ‚‚)
    : prod.lift gâ‚ gâ‚‚ â‰« prod.map fâ‚ fâ‚‚ = prod.lift (gâ‚ â‰« fâ‚) (gâ‚‚ â‰« fâ‚‚) := by
  rw [
    map_def,
    ex1.a,
    â†Category.assoc, â†Category.assoc,
    prod.lift_fst, prod.lift_snd]

theorem ex1.c1
    (fâ‚ : Yâ‚ âŸ¶ Zâ‚) (fâ‚‚ : Yâ‚‚ âŸ¶ Zâ‚‚)
    (gâ‚ : Xâ‚ âŸ¶ Yâ‚) (gâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚)
    : prod.map gâ‚ gâ‚‚ â‰« prod.map fâ‚ fâ‚‚ = prod.map (gâ‚ â‰« fâ‚) (gâ‚‚ â‰« fâ‚‚) := by
  rw [
    map_def,
    map_def,
    ex1.a,
    â†Category.assoc, â†Category.assoc,
    prod.lift_fst, prod.lift_snd,
    Category.assoc, Category.assoc,
    â†map_def,
  ]

theorem ex1.c2
    : prod.map (ğŸ™ Xâ‚) (ğŸ™ Xâ‚‚) = ğŸ™ _ := by
  rw [
    map_def,
    Category.comp_id, Category.comp_id,
    prod.lift_fst_snd
  ]

def AnUnit (_x : Type u) := PUnit

instance {Î± : Type u} [Monoid Î±] : Category (AnUnit Î±) where
  Hom a b := Î±
  id x := 1
  comp a b := b * a
  assoc f g h := Eq.symm (mul_assoc h g f)

end ex1

theorem ex2 {Î±}
    [Monoid Î±]
    (op : Î± â†’ Î± â†’ Î±)
    (pâ‚ pâ‚‚ : Î±)
    (hâ‚ : âˆ€ x y, pâ‚ * (op x y) = x)
    (hâ‚‚ : âˆ€ x y, pâ‚‚ * (op x y) = y)
    (hâ‚ƒ : op pâ‚ pâ‚‚ = 1)
    (hâ‚„ : âˆ€ x y z, (op x y) * z = op (x * z) (y * z))
    (a b : AnUnit Î±)
    : HasBinaryProduct a b :=
  IsBinaryProduct.hasBinaryProduct (P := .unit) pâ‚ pâ‚‚
  <| IsBinaryProduct.ofUniqueHom op hâ‚ hâ‚‚
    fun (f g m : Î±) (hf : pâ‚ * m = f) (hg : pâ‚‚ * m = g) => calc
      m = 1 * m                 := Eq.symm (one_mul m)
      _ = op pâ‚ pâ‚‚ * m          := by rw [hâ‚ƒ]
      _ = op (pâ‚ * m) (pâ‚‚ * m)  := by rw [hâ‚„]
      _ = op f g                := by rw [hf, hg]

noncomputable section ex3

variable (X Y Z)
    [HasBinaryProduct X Y]
    [HasBinaryProduct Y Z]
    [HasBinaryProduct X (Y â¨¯ Z)]
    [HasBinaryProduct (X â¨¯ Y) Z]
    [HasTerminal ğ“’]
    [HasBinaryProduct X (âŠ¤_ ğ“’)]
    [HasBinaryProduct (âŠ¤_ ğ“’) X]
    [HasBinaryProduct Y X]

def Î± : (X â¨¯ Y) â¨¯ Z â‰… X â¨¯ (Y â¨¯ Z) where
  hom := prod.lift (prod.fst â‰« prod.fst) (prod.map prod.snd (ğŸ™ _))
  inv := prod.lift (prod.map (ğŸ™ _) prod.fst) (prod.snd â‰« prod.snd)

def Â«Î»Â» : X â¨¯ âŠ¤_ _ â‰… X where
  hom := prod.fst
  inv := prod.lift (ğŸ™ X) (terminal.from X)

def Ï : (âŠ¤_ _) â¨¯ X â‰… X where
  hom := prod.snd
  inv := prod.lift (terminal.from X) (ğŸ™ X)

def Ï„ : X â¨¯ Y â‰… Y â¨¯ X where
  hom := prod.lift prod.snd prod.fst
  inv := prod.lift prod.snd prod.fst

end ex3

section ex4a

variable
  [HasBinaryProducts ğ“’]

noncomputable def pow (O : ğ“’) : Nat â†’ ğ“’
  | 0 => O
  | n+1 => O â¨¯ pow O n

lemma finsetGeneration'
    {A : Type u}
    [Fintype A]
    (f : Nat â†’ A)
    : âˆƒ n, âˆƒ k, n â‰  k âˆ§ f n = f k := by
  by_contra! h
  exact not_injective_infinite_finite f
    <| Function.injective_iff_pairwise_ne.mpr h

lemma finsetGeneration
    {A : Type u}
    [Fintype A]
    (f : Nat â†’ A)
    : âˆƒ n, âˆƒ k, n < k âˆ§ f n = f k := by
  have âŸ¨a, b, hneq, heqâŸ©:= finsetGeneration' f
  by_cases hlt : a < b
  Â· use a, b
  Â· have hlt : b < a := by omega
    use b, a
    exact âŸ¨hlt, heq.symmâŸ©

noncomputable def pow_list (ls : Finset (A âŸ¶ B)) : (n : Nat) â†’ Finset (A âŸ¶ pow B n)
  | 0 => ls
  | n+1 =>
    ls.disjiUnion (fun f => (pow_list ls n).map âŸ¨
      (prod.lift f),
      fun x y heq => by 
        have âŸ¨_, heqâŸ©:= Limits.prod.hom_ext_iff.mp heq
        rwa [prod.lift_snd, prod.lift_snd] at heq
    âŸ©) fun a amem b bmem hneq v vamem vbmem z zmem => by
        exfalso
        specialize vamem zmem
        specialize vbmem zmem
        simp at vamem vbmem
        rcases vamem with âŸ¨wa, wamem, rflâŸ©
        rcases vbmem with âŸ¨wb, wbmem, hFalseâŸ©
        have âŸ¨hFalse, _âŸ© := Limits.prod.hom_ext_iff.mp hFalse
        simp at hFalse
        exact hneq hFalse.symm

theorem pow_list.card
    (ls : Finset (A âŸ¶ B))
    : (n : Nat) â†’ (pow_list ls n).card = ls.card ^ n.succ
  | 0 => by simp [pow_list]
  | n+1 => by simp [pow_list, pow_list.card _ n, â†Nat.pow_add_one']

theorem pow_list.allMem
    (ls : Finset (A âŸ¶ B))
    (hAll : âˆ€ v, v âˆˆ ls)
    : (n : Nat) â†’ âˆ€ v, v âˆˆ (pow_list ls n)
  | 0,   v => hAll v
  | n+1, v => by
    simp only [pow_list, Finset.mem_disjiUnion, Finset.mem_map, Function.Embedding.coeFn_mk]
    use v â‰« prod.fst, hAll _, v â‰« prod.snd, pow_list.allMem _ hAll n _
    calc
      _ = _                       := by rw [â† @prod.comp_lift]
      _ = v â‰« ğŸ™ (pow B (n + 1))   := by rw [@prod.lift_fst_snd]; rfl
      _ = v                       := by rw [Category.comp_id v]

theorem ex4.a
    [objFin : Fintype ğ“’]
    [morphFin : âˆ€ A B : ğ“’, Fintype (A âŸ¶ B)]
    : âˆ€ A B : ğ“’, Subsingleton (A âŸ¶ B) := by
  have deq : DecidableEq ğ“’ := Classical.typeDecidableEq ğ“’
  by_contra! h
  simp only [subsingleton_iff, not_forall] at h
  rcases h with âŸ¨A,B,f,g,hneqâŸ©
  have deqhom: DecidableEq (A âŸ¶ B) := Classical.typeDecidableEq _
  have mf := morphFin A B
  have âŸ¨n, k, hlt, heqâŸ©:= finsetGeneration (pow B)
  have hLpln := pow_list.card mf.elems n
  have hLplk := pow_list.card mf.elems k
  have hMpln := pow_list.allMem mf.elems Fintype.complete n
  have hMplk := pow_list.allMem mf.elems Fintype.complete k
  have : (pow_list mf.elems n).card = (pow_list mf.elems k).card := by
    apply Finset.card_bijective (Â· â‰« eqToHom heq) 
    Â· refine Function.bijective_iff_has_inverse.mpr ?_
      use (Â· â‰« eqToHom (Eq.symm heq))
      constructor <;> intro _ <;> simp
    Â· intro i
      exact âŸ¨fun a â†¦ hMplk _, fun a â†¦ hMpln _âŸ©
  have mfLenNT : 2 â‰¤ mf.elems.card := by 
    rw [â†Finset.card_pair hneq]
    apply Finset.card_le_card
    exact fun x _ => Fintype.complete x
  have hFalse : Nat.pow _ _ = Nat.pow _ _ := hLpln.symm.trans this |>.trans hLplk
  generalize mf.elems.card = c at *
  clear *-hFalse mfLenNT hlt
  have := (Nat.pow_right_inj (by omega)).mp hFalse
  omega

end ex4a

section ex4b

instance : Category (Sigma Fintype) where
  Hom a b := a.fst â†’ b.fst
  id X := _root_.id
  comp a b := Function.comp b a

variable {X Y : Sigma Fintype}

instance : Fintype X.fst := X.snd

noncomputable instance : Fintype (X âŸ¶ Y) := by 
  have : DecidableEq X.fst := Classical.typeDecidableEq _
  have : DecidableEq Y.fst := Classical.typeDecidableEq _
  change Fintype (X.fst â†’ Y.fst)
  infer_instance

instance : HasBinaryProduct X Y := 
  IsBinaryProduct.hasBinaryProduct (P := âŸ¨ X.fst Ã— Y.fst, inferInstance âŸ©)
    Prod.fst Prod.snd <|
  IsBinaryProduct.ofUniqueHom 
    (fun x y v => âŸ¨x v, y vâŸ©)
    (fun _ _ => rfl)
    (fun _ _ => rfl)
    (fun f g m hf hg => by 
      funext v
      ext
      Â· change (Prod.fst âˆ˜ m) _ = f v
        exact congrFun hf v
      Â· change (Prod.snd âˆ˜ m) _ = g v
        exact congrFun hg v)

instance : HasBinaryProducts (Sigma Fintype) :=
  hasBinaryProducts_of_hasLimit_pair _

end ex4b

end product

section coproduct

section ex1

instance hamon : Category (Sigma AddCommMonoid) where
  Hom := fun âŸ¨s, isâŸ© âŸ¨t, itâŸ© => AddMonoidHom s t
  id  := fun âŸ¨v, ivâŸ© => AddMonoidHom.id v
  comp := fun {a b c} f g =>
    have âŸ¨a, iaâŸ© := a
    have âŸ¨b, ibâŸ© := b
    have âŸ¨c, icâŸ© := c
    (AddMonoidHom.comp (g : _ â†’+ _) (f : _ â†’+ _) : _ â†’+ _)

variable {A B : Sigma AddCommMonoid} {X Y : Type _}
    [AddMonoid X] [AddMonoid Y]

instance : AddCommMonoid A.fst := A.snd

instance : HasBinaryProduct A B := 
  IsBinaryProduct.hasBinaryProduct (P := âŸ¨ A.fst Ã— B.fst, by infer_instanceâŸ©)
    (.fst _ _) (.snd _ _) <|.ofUniqueHom
    (fun f g => AddMonoidHom.prod f g)
    (fun f g => rfl)
    (fun f g => rfl)
    (fun f g m => by
      rintro rfl rfl; dsimp
      exact AddMonoidHom.prod_unique m)

def amInl : X â†’+ X Ã— Y where
  toFun x := âŸ¨x, 0âŸ©
  map_zero':= rfl
  map_add' x y := by simp

def amInr : Y â†’+ X Ã— Y where
  toFun x := âŸ¨0, xâŸ©
  map_zero':= rfl
  map_add' x y := by simp


instance : HasBinaryCoproduct A B := 
  IsBinaryCoproduct.hasBinaryCoproduct (P := âŸ¨ A.fst Ã— B.fst, by infer_instanceâŸ©)
    amInl amInr <|.ofUniqueHom
      (fun a b => {
        toFun := fun âŸ¨x, yâŸ© => a.toFun x + b.toFun y
        map_add' := by 
          rintro âŸ¨xâ‚, xâ‚‚âŸ© âŸ¨yâ‚, yâ‚‚âŸ©
          dsimp
          calc 
            a.toFun (xâ‚ + yâ‚) + b.toFun (xâ‚‚ + yâ‚‚)
              = a.toFun xâ‚ + a.toFun yâ‚ + (b.toFun xâ‚‚ + b.toFun yâ‚‚) := by simp
            _ = a.toFun xâ‚ + (a.toFun yâ‚ + b.toFun xâ‚‚) + b.toFun yâ‚‚ := by simp only [add_assoc]
            _ = a.toFun xâ‚ + (b.toFun xâ‚‚ + a.toFun yâ‚) + b.toFun yâ‚‚ := by nth_rw 3 [add_comm]
            _ = a.toFun xâ‚ + b.toFun xâ‚‚ + (a.toFun yâ‚ + b.toFun yâ‚‚) := by simp only [add_assoc]
        map_zero' := by simp
      })
      (fun f g => AddMonoidHom.ext fun x => by 
        simp [CategoryStruct.comp, amInl])
      (fun f g => AddMonoidHom.ext fun x => by 
        simp [CategoryStruct.comp, amInr])
      (fun f g m => by
        rintro rfl rfl; refine AddMonoidHom.ext fun âŸ¨x, yâŸ© => ?_
        calc
          m.toFun (x, y)
            = m.toFun ((x, 0) + (0, y))        := by simp
          _ = m.toFun (x, 0) + m.toFun (0, y)  := m.map_add _ _)

end ex1

class Distributive ğ“’ [Category ğ“’] [HasBinaryProducts ğ“’] [HasBinaryCoproducts ğ“’] where
  dist : âˆ€ X Y Z : ğ“’, (X â¨¯ Y) â¨¿ (X â¨¯ Z) âŸ¶ X â¨¯ (Y â¨¿ Z)
  dist_uniq : âˆ€ X Y Z d,
    prod.map (ğŸ™ X) coprod.inl = coprod.inl â‰« d â†’
      prod.map (ğŸ™ X) coprod.inr = coprod.inr â‰« d â†’
        dist X Y Z = d

section ex2

variable [Category ğ“’] [HasBinaryProducts ğ“’] [HasBinaryCoproducts ğ“’] [Distributive ğ“’]

end ex2

end coproduct

