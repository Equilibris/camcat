import Mathlib.Logic.ExistsUnique
import Mathlib.Logic.Equiv.Defs
import Mathlib.Logic.Equiv.Nat
import Mathlib.Data.Rat.Denumerable
import Mathlib.Tactic.Group
import Mathlib.Tactic.DepRewrite
import Mathlib.Algebra.Group.Defs
import Mathlib.CategoryTheory.Category.Basic
import Mathlib.CategoryTheory.Category.Cat.Terminal
import Mathlib.CategoryTheory.Iso
import Mathlib.LinearAlgebra.Matrix.NonsingularInverse
import Mathlib.Logic.Basic
import Mathlib.Logic.Relation

universe u v

namespace CategoryTheory

section Ex1

-- We consider some general category ğ“’
variable {ğ“’ : Type u} [Category ğ“’]

-- And some objects in ğ“’
variable {X Y Z A B C U V W L : ğ“’}

-- Along with some morphisms
variable {f g : X âŸ¶ Y}

-- A morphism f : X âŸ¶ Y is a monomorphism if it is left-cancellabe.
-- Here is the definition provided by mathlib.
/--
info: constructor CategoryTheory.Mono.mk.{v, u} : âˆ€ {C : Type u} [inst : Category.{v, u} C] {X Y : C} {f : X âŸ¶ Y},
  (âˆ€ {Z : C} (g h : Z âŸ¶ X), g â‰« f = h â‰« f â†’ g = h) â†’ Mono f
-/
#guard_msgs in
#print Mono.mk

section Ex1_1

-- A morphism s : X âŸ¶ Y is a *section* if:
class Section (s : X âŸ¶ Y) where
  -- There exists some morphism
  r : Y âŸ¶ X
  -- such that
  s_r_involution : s â‰« r = ğŸ™ X
  -- NOTE! We write s â‰« r for r âˆ˜ s due to mathlib prefering this

instance
    -- For any
    {s : X âŸ¶ Y}
    -- satisfying
    [hSec : Section s]
    -- it follows that it is a
    : Mono s where
  right_cancellation
      -- For some object
      {Z}
      -- and morphisms
      (g h : Z âŸ¶ X)
      -- satisfying
      (heq : g â‰« s = h â‰« s)
      -- we are RTP
      : g = h := calc
    g = g â‰« ğŸ™ X           := (Category.comp_id g).symm
    _ = g â‰« (s â‰« hSec.r)  := by rw [hSec.s_r_involution]
    _ = (g â‰« s) â‰« hSec.r  := (Category.assoc g s hSec.r).symm
    _ = (h â‰« s) â‰« hSec.r  := by rw [heq]
    _ = h â‰« (s â‰« hSec.r)  := (Category.assoc h s hSec.r)
    _ = h â‰« ğŸ™ X           := by rw [hSec.s_r_involution]
    _ = h                 := (Category.comp_id h)

end Ex1_1

section Ex1_2

-- An
class Equalizer
    {L X Y : ğ“’}
    -- is a morphism
    (l : (L âŸ¶ X))
    -- over some pair
    (f g : (X âŸ¶ Y))
    where
  -- whenever
  leq : l â‰« f = l â‰« g
  -- and
  uniq
    -- for every object and morphism
    {K} (k : K âŸ¶ X)
    -- satisfying
    : k â‰« f = k â‰« g
    -- we can conclude
    â†’ âˆƒ! u : K âŸ¶ L, u â‰« l = k

instance Equalizer.mono
    {L X Y : ğ“’} {f g : X âŸ¶ Y} {l : L âŸ¶ X}
    (hEqz : Equalizer l f g)
    : Mono l where
  right_cancellation
      -- For some object
      {Z}
      -- and morphisms
      (u v : Z âŸ¶ L)
      -- satisfying
      (heq : u â‰« l = v â‰« l)
      -- we are RTP
      : u = v :=

    -- We can first conclude that
    have ulfEqUlg : (u â‰« l) â‰« f = (u â‰« l) â‰« g := calc
      (u â‰« l) â‰« f
        = u â‰« (l â‰« f) := Category.assoc u l f
      _ = u â‰« (l â‰« g) := by rw [hEqz.leq]
      _ = (u â‰« l) â‰« g := (Category.assoc u l g).symm

    -- Then we can see we can instantiate hEqz.uniq
    -- using the equality we just showed.
    have âŸ¨
      -- This gives us the morphism
      (w : Z âŸ¶ L),
      -- satisfying
      (wlEqUl : w â‰« l = u â‰« l),
      -- which is also unique.
      -- It is just the uniqueness we need.
      (huniq : âˆ€ (y : Z âŸ¶ L), y â‰« l = u â‰« l â†’ y = w)
    âŸ© := hEqz.uniq (u â‰« l) ulfEqUlg

    calc
      u = w :=
        -- trivially, since u â‰« l = u â‰« l,
        -- we can conclude u = w.
        huniq u rfl
      w = v :=
        -- then using the assumtion heq : u â‰« l = v â‰« l
        -- we can conclude v = w.
        (huniq v heq.symm).symm

end Ex1_2

section Ex1_3

class Regular (l : L âŸ¶ X) where
  (Y : ğ“’)
  (f g : X âŸ¶ Y)
  hEqz : Equalizer l f g

instance
    {s : L âŸ¶ X}
    [hSec : Section s]
    : Regular s where
  -- We pick
  Y := X
  -- and the morphisms
  f := ğŸ™ X
  g := hSec.r â‰« s

  -- Now it remains to show s forms an equalizer ğŸ™ X and Section.r s â‰« s.
  hEqz := {
    -- We construct the equality proof.
    leq := (calc
      s â‰« ğŸ™ X
        = s                 := Category.comp_id s
      _ = ğŸ™ L â‰« s           := (Category.id_comp s).symm
      _ = (s â‰« hSec.r) â‰« s  := by rw [hSec.s_r_involution]
      _ = s â‰« hSec.r â‰« s    := Category.assoc s (Section.r s) s)
    -- Now it remains to show that s is unique.
    uniq
        {K}
        (k : K âŸ¶ X)
        (heq : k â‰« ğŸ™ X = k â‰« hSec.r â‰« s)
        : âˆƒ! x, x â‰« s = k := by
      -- We begin by chancing the goal using the assumtion.
      rw [show k = k â‰« hSec.r â‰« s from (Category.comp_id _).symm.trans heq]
      clear heq
      -- Now we are RTP: âˆƒ! x, x â‰« s = k â‰« Section.r s â‰« s
      exact âŸ¨
        -- We pick the structure that makes the equality the easiest.
        k â‰« hSec.r,
        -- This collapses the equality into simply associativity
        Category.assoc k (Section.r s) s,
        -- Finally now to show the uniqueness follows cleanly
        fun y (hyeq : y â‰« s = k â‰« hSec.r â‰« s) => calc
          y = y â‰« ğŸ™ L                   := (Category.comp_id y).symm
          _ = y â‰« (s â‰« hSec.r)          := by rw [hSec.s_r_involution]
          _ = (y â‰« s) â‰« hSec.r          := (Category.assoc y s hSec.r).symm
          _ = (k â‰« hSec.r â‰« s) â‰« hSec.r := by rw [hyeq]
          _ = k â‰« hSec.r â‰« (s â‰« hSec.r) := by simp only [Category.assoc]
          _ = k â‰« hSec.r â‰« ğŸ™ _          := by rw [hSec.s_r_involution]
          _ = (k â‰« hSec.r) â‰« ğŸ™ _        := (Category.assoc k hSec.r (ğŸ™ L)).symm
          _ = k â‰« hSec.r                := Category.comp_id (k â‰« Section.r s)
      âŸ©
  }

end Ex1_3

section Ex1_4

class Strong (m : X âŸ¶ Y) where
  strong :
    âˆ€ {U V},
    âˆ€ e : U âŸ¶ V,
    âˆ€ u v,
    Epi e â†’ e â‰« v = u â‰« m â†’ âˆƒ! d : V âŸ¶ X, u = e â‰« d âˆ§ d â‰« m = v

instance
    {m : X âŸ¶ Y}
    [hReg : Regular m]
    : Strong m where
  strong {U V} e u v eepi hComm := by
    have âŸ¨
      -- We begin by using the fact that any equalizer is a monomorphism.
      -- This will be used to right cancel m.
      (hRightCancel : âˆ€ {Z : ğ“’} (g h : Z âŸ¶ X), g â‰« m = h â‰« m â†’ g = h)
    âŸ© := Equalizer.mono hReg.hEqz

    -- We expand the hypothesis into its induvidual parts.
    -- This is done to just save space.
    rcases hReg with âŸ¨
      -- Call the object
      Z,
      -- and the morphisms
      f,
      g,
      -- We get that the expected square commutes
      mfEqMg : m â‰« f = m â‰« g,
      -- along with its uniqueness.
      -- This uniqueness will generate the needed morphism
      uniq : âˆ€ {K} (k : K âŸ¶ Y), k â‰« f = k â‰« g â†’ âˆƒ! u, u â‰« m = k
    âŸ©

    -- This equality will be used to instantiate the uniqueness just above.
    -- The proof proceeds usind the left cancellation of e
    have vfEqVg : v â‰« f = v â‰« g := eepi.left_cancellation (v â‰« f) (v â‰« g) <|
      -- This proof can be made much simpler (by simp [hComm, mfEqMg]),
      -- but I keep it in the calculative format to make it more visible.
      calc
        e â‰« v â‰« f
          = (e â‰« v) â‰« f := (Category.assoc e v f).symm
        _ = (u â‰« m) â‰« f := by rw [hComm]
        _ = u â‰« (m â‰« f) := (Category.assoc _ _ _)
        _ = u â‰« (m â‰« g) := by rw [mfEqMg]
        _ = (u â‰« m) â‰« g := (Category.assoc _ _ _).symm
        _ = (e â‰« v) â‰« g := by rw [hComm]
        _ = e â‰« v â‰« g   := Category.assoc e v g

    obtain âŸ¨
      -- We construct the morphism
      (w : V âŸ¶ X),
      -- this syntax eliminates the equality so we are effectively rewriting with it
      (rfl : w â‰« m = v),
      -- We are then also given the uniqueness bellow.
      -- This will lift exactly to the uniqueness needed for the proof.
      (huniq : âˆ€ x, x â‰« m = w â‰« m â†’ x = w)
    âŸ© := uniq v vfEqVg; clear uniq

    -- Finally, now we are RTP: âˆƒ! d, u = e â‰« d âˆ§ d â‰« m = w â‰« m
    refine âŸ¨
      -- We use the constructed morphism as d
      w,
      âŸ¨
        -- Applying the right cancellation, we're RTP: u â‰« m = (e â‰« w) â‰« m
        hRightCancel u (e â‰« w) ?_,
        -- because of the elimination (rfl : w â‰« m = v)
        rfl
      âŸ©,
      -- The uniqueness lifts exactly as one would expect.
      -- We can see that hr is exactly the square we need 
      -- (thanks to the equality elimination above).
      fun y âŸ¨_, (hr : y â‰« m = w â‰« m)âŸ© => huniq y hr
    âŸ©
    -- Finally, showing u â‰« m = (e â‰« w) â‰« m
    -- is the only thing that remains
    calc
      u â‰« m
        = e â‰« w â‰« m := hComm.symm
      _ = (e â‰« w) â‰« m := (Category.assoc _ _ _).symm

end Ex1_4

section Ex1_5

class Extremal (m : X âŸ¶ Y) where
  extreme :
    âˆ€ {V},
    âˆ€ e : X âŸ¶ V,
    âˆ€ v : V âŸ¶ Y,
    Epi e â†’ e â‰« v = m â†’ IsIso e

instance
    {m : X âŸ¶ Y}
    [hStrong : Strong m]
    : Extremal m where
  extreme {V} e v eepi eeq :=
    -- We see that using the strong morphism we can construct
    have âŸ¨
      -- the inverse morphism w
      (w : V âŸ¶ X),
      -- along with an equation showing it forms an inverse.
      âŸ¨(hinv : ğŸ™ X = e â‰« w), _âŸ©,
      _
    âŸ© :=
      -- To do this instation we use e and the ğŸ™ X morphisms.
      -- We pick e as it is the only morphism we know is Epi,
      -- and we pick ğŸ™ X as it forces the equation into the form we need.
      hStrong.strong e (ğŸ™ X) v
      eepi
      -- The equation we need to provide turns out to come from our assumptions.
      (eeq.trans (Category.id_comp _).symm)
    âŸ¨
      -- We pick the inverse weve constructed
      w,
      -- and naturally the first equation is exactly what we need
      hinv.symm,
      -- we are not RTP: w â‰« e = ğŸ™ V
      -- We note we have the equation (hinv : ğŸ™ X = e â‰« w),
      -- using this along with the fact that e is an Epi,
      -- we can do the equational resoning.
      eepi.left_cancellation _ _ <|
        calc
          e â‰« w â‰« e
            = (e â‰« w) â‰« e := (Category.assoc e w e).symm
          _ = ğŸ™ X â‰« e     := by rw [hinv]
          _ = e           := Category.id_comp e
          _ = e â‰« ğŸ™ V     := (Category.comp_id e).symm
    âŸ©

end Ex1_5

end Ex1

end CategoryTheory

